// BASIC OWL MAPPING

// here we ignore all temporal arguments

// is_a, instance_of

// DECLARATIONS
(forall (p)
        (iff (relation p)
             (owl:ObjectProperty p)))

(forall (c)
        (iff (type c)
             (owl:Class c)))

(forall (i)
        (iff (instance i)
             (owl:individual i)))

// PROPERTY CHARACTERISTICS (UNARY)
(forall (p)
        (iff (transitive p)
             (owl:TransitiveProperty p)))

(forall (p)
        (iff (symmetric p)
             (owl:SymmetricProperty p)))

(forall (p)
        (iff (asymmetric p)
             (owl:AsymmetricProperty p)))

(forall (p)
        (iff (functional p)
             (owl:FunctionalProperty p)))

(forall (p)
        (iff (inverse_functional p)
             (owl:InverseFunctionalProperty p)))

(forall (p)
        (iff (irreflexive p)
             (owl:IrreflexiveProperty p)))

(forall (p)
        (if (owl:ReflexiveProperty p)
            (weakly_reflexive p)))

// PROPERTY CHARACTERISTICS (BINARY)

(forall (p q)
        (iff (inverse_of p q)
             (owl:inverseProperties p q)))

(forall (p q)
        (iff (domain p q)
             (owl:PropertyDomain p q)))

(forall (p q)
        (iff (range p q)
             (owl:PropertyRange p q)))

(forall (p q)
        (iff (subrelation p q)
             (owl:SubPropertyOf p q)))

// CHAINS
(forall (p q)
        (iff (transitive_over p q)
             (owl:SubPropertyOf (owl:PropertyChain p q) p)))

(forall (p q r)
        (iff (holds_over_chain p q r)
             (owl:SubPropertyOf (owl:PropertyChain q r) p)))


// GCIs
(forall (r A)
        (if (homeomorphic_for r A)
            (owl:SubClassOf A (owl:allValuesFrom r A))))



